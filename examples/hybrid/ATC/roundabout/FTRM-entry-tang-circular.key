\programVariables {
R x1,x2, d1,d2;
R om, c1,c2;
R h1,h2;
R r;
}
/**
 * Flyable Tangential Roundabout Maneuver in Air Traffic Control (ATC)
 * Shows that entry procedure follows a circular path motion
 * @author Andre Platzer
 * @see "Andre Platzer and Edmund M. Clarke. Formal verification of curved flight collision avoidance maneuvers: A case study. In Ana Cavalcanti and Dennis Dams, editors, 16th International Symposium on Formal Methods, FM, Eindhoven, Netherlands, Proceedings, volume 5850 of LNCS, pages 547-562. Springer, 2009."
 */
\problem {
  \[  c1:=0;c2:=0;om:=1 \] (
    (r*om)^2=d1^2+d2^2
    /*& (x1-c1)^2+(x2-c2)^2=3*r^2
    & \exists R t; (t>=0 & x1+t*d1=c1 & x2+t*d2=c2)*/
   ->
    \[h1:=*;h2:=*;
      ?(d1=-(-om)*(x2-h2)&d2=-om*(x1-h1));
      /*@ignore for performance ?((h1-c1)^2+(h2-c2)^2=(2*r)^2);*/
      {x1`=d1,x2`=d2, d1`=-(-om)*d2,d2`=-om*d1,
       ((x1-c1)^2+(x2-c2)^2>=r^2)}
      @invariant(d1=-(-om)*(x2-h2)&d2=-om*(x1-h1),(x1-h1)^2+(x2-h2)^2=r^2)
    \] (x1-h1)^2+(x2-h2)^2=r^2
 )
}
