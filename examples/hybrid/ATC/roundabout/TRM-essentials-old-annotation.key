\programVariables {
  R x1,x2, d1,d2;
  R y1,y2, e1,e2;
  R om, omy, c1,c2;
}
/**
 * Essentials of Tangential Roundabout Maneuver (TRM) in Air Traffic Control (ATC)
 * @author Andre Platzer
 * @see "Andre Platzer and Edmund M. Clarke. Formal verification of curved flight collision avoidance maneuvers: A case study. In Ana Cavalcanti and Dennis Dams, editors, 16th International Symposium on Formal Methods, FM, Eindhoven, Netherlands, Proceedings, volume 5850 of LNCS, pages 547-562. Springer, 2009."
 */
\functions{
  R protectedzone;
}

/**
 * Provable using
 *  diff_weaken for free flight goal
 *  diff_strengthen for TRM goal with
 *    either (d1-e1=-om*(x2-y2)&d2-e2=om*(x1-y1))
 *    or     (d1=-om*(x2-c2)&d2=om*(x1-c1) & e1=-om*(y2-c2)&e2=om*(y1-c1))
 */
\problem {
    (x1-y1)^2 + (x2-y2)^2 >= protectedzone^2
  ->
   \[(
       (
         om:=*;omy:=*;
         {x1'=d1,x2'=d2, d1'=-om*d2,d2'=om*d1, y1'=e1,y2'=e2, e1'=-omy*e2,e2'=omy*e1,
          ((x1-y1)^2 + (x2-y2)^2 >= protectedzone^2)}@weaken()
       )*@invariant((x1-y1)^2 + (x2-y2)^2 >= protectedzone^2);
       c1:=*;c2:=*; om:=*;
       d1:=-om*(x2-c2); d2:=om*(x1-c1);
       e1:=-om*(y2-c2); e2:=om*(y1-c1);
       {x1'=d1,x2'=d2, d1'=-om*d2,d2'=om*d1, y1'=e1,y2'=e2, e1'=-om*e2,e2'=om*e1}
       @strengthen(d1-e1=-om*(x2-y2)&d2-e2=om*(x1-y1))
    )*@invariant((x1-y1)^2 + (x2-y2)^2 >= protectedzone^2)\] (
        (x1-y1)^2 + (x2-y2)^2 >= protectedzone^2
    )
}
