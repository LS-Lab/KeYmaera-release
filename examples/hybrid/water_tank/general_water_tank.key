\sorts{
R;
}

\functions {
R f; /* fill */
R d; /* drain */
R max_fill;
R min_fill;
R max_com;
}

\problem {
\[ R x; R y; R st; x:=0; st:=0 \] ( (st = 0 & y = min_fill & f > 0 & d > 0 & max_com >= 0 & min_fill >= 0 & max_fill > min_fill + f*max_com & min_fill < max_fill - d*max_com) & min_fill + f * max_com < max_fill - d*max_com
-> 
 \[
(
  (?(st=0);
	    (
		  (?y>=min_fill + d*max_com; x:=0; st:=1)
		++
		  {y'=f, y<=max_fill - f * max_com}
		)
  )
++
  (?(st=1);
	     (
		   st:=2
	     ++ 
		   {x'=1,y'=f, x <= max_com}
		 )
  )
++ (?(st=2);
		( 
		  (?y<= max_fill - f * max_com; x:=0; st:=3)
		++
          {y'=-d, y>=min_fill + d * max_com}
		)
   )
++ (?(st=3);
       (
	     (st:=0)
       ++
	     {x'=1,y'=-d, x <= max_com}
	   )
   )
)*@invariant((st = 0 | st = 1 | st = 2 | st =3)
&
x >= 0 & x <= max_com
&
(st =0 -> (y >= min_fill & y <= max_fill - f*max_com))
&
(st = 1 -> (y >= min_fill+d*max_com & y <= max_fill - f*(max_com - x)))
&
(st =2 -> (y >= min_fill+d*max_com & y <= max_fill))
&
(st = 3 -> (y >= min_fill + d*(max_com-x) & y <= max_fill - f*max_com)))
\] 
(y >= min_fill & y <= max_fill))
}
