/* s0805753@sms.ed.ac.uk, University of Edinburgh
 *
 * DESCRIPTION
 *
 *  Stick mode in the 3-state hybrid automaton model of a conventional oil well 
 *  drill string, as presented in A.N.Lopez & R. Carter in "Hybrid automata: an
 *  insight into the discrete abstraction of discontinuous systems",
 *  International Journal of Systems Science, August 2010.
 * 
 *  This file can prove global stability of the equilibrium in the  mode where 
 *  the drill is stuck. One can either proceed with a Lyapunov  proof 
 *  (the Lyapunov function is already given in the description) or by solving 
 *  the system of ODEs and using MetiTarski on the solution. The latter
 *  requires a preliminary Simplify step to be performed, which will eliminate
 *  occurrences of Sin[] and Cos[] and yield a formula in Exp[]; the resulting 
 *  goal can be closed using MetiTarski.
 */

\functions {
\external R E;
\external R Exp(R);
\external R Sin(R);
\external R Cos(R);
\external R Tan(R);
R a;
}

\programVariables {
   R x1, x2, Jr, Jb, Rb, kt, ct, cr, cb, mucb, musb, delta, gammab, nuf, Wob,u;
}

\problem {

   /* CONSTANTS */
   cb=50        &    /* CONSTANT USED TO APPOXIMATE THE INFLUENCE OF MUD ON THE BIT (Nms/rad) (c_{b}\dot{\phi_{b}}) */
   kt=861.5336  &    /* TORSIONAL STIFFNESS (Nm/rad) */
   Jr=2212      &    /* TOP ROTARY INERTIA (kgm^2) */
   Jb=471.9698  &    /* BIT ROTARY INERTIA (kgm^2) */
   Rb=0.155575  &    /* RADIUS OF THE BIT (m) */
   ct=172.3067  &    /* TORSIONAL DAMPING (Nms/rad) */
   cr=425       &    /* CONSTANT USED TO CALCULATE THE VISCOUS DAMPING TORQUE OF THE
                        TOP ROTARY SYSTEM (Nms/rad), i.e. T_{r}(\dot{\phi_{r}})=c_{r}\dot {\phi_{r}}  */
   mucb=0.5     &    /* STATIC FRICTION COEFFICIENT ASSOCIATED WITH THE BIT INERTIA Jb, \mu_{sb}\in (0,1) */
   musb=0.8     &    /* COULOMB FRICTION COEFFICIENT ASSOCIATED WITH THE BIT INERTIA Jb, \mu_{cb}\in (0,1) */
   delta=0.000001 &  /* IF THE VELOCITY IS LESS THAN delta, THE SYSTEM CAN EITHER BE STUCK OR IN TRANSITION */
   gammab=0.9   &    /* CONSTANT USED TO CALCULATE THE FRICTION TORQUE, \gamma_{b}\in (0,1) */
   nuf=1        &    /* CONSTANT USED TO CALCULATE THE FRICTION TORQUE,  \nu_{f}>0 */

   /* CONTROL PARAMETERS */
   Wob=50000    &
   u=6000       &

(((2765000*x1^2)/1076917 + (-(7500000/1076917) + x2)^2) <= a ) & a>=0 -> 
   \[ 
      {x1' = (-(ct + cr)*x1 - kt*x2 + u)/Jr , x2' = x1} 
   \] 
((2765000*x1^2)/1076917 + (-(7500000/1076917) + x2)^2) <= a
}
