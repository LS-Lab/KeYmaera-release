/* s0805753@sms.ed.ac.uk, University of Edinburgh.
 * 
 * DESCRIPTION
 *   This example provides an estimate to the domain of attraction of a 
 *   whirling pendulum, as presented by Graziano Chesi in "Estimating 
 *   the Domain of Attraction for Non-Polynomial Systems via LMI 
 *   optimizations", University of Hong Kong.
 *   
 *   This example is provable using differential induction DI.
 *   MetiTarski settings require QEPCAD to be set as the EADM.
 */

\functions {
\external R E;
\external R Exp(R);
\external R Sin(R);
\external R Cos(R);
\external R Tan(R);
R a;
}

\programVariables {
R x1, x2, kf, mb, omega, g, lp;
}

\problem {
   kf    = 0.2 & /* FRICTION */
   mb    = 1   & /* MASS OF THE RIGID ARM */
   omega = 0.9 & /* ROTATING ANGULAR VELOCITY */
   g     = 10  & /* GRAVITY ACCELERATION */
   lp    = 10  & /* LENGTH OF THE RIGID ARM */

 ((x1^2 +x1*x2 +4*(x2^2)) <= a ) & a>=0  /* LYAPUNOV FUNCTION */ -> 

   \[ 
      { x1' = x2, 
        x2' = -(kf/mb)*x2 + (omega^2)*Sin(x1)*Cos(x1) - (g/lp)*Sin(x1) &
        ((x1^2 +x1*x2 +4*(x2^2)) <= 0.69929971)
      } 
   \] 

 ((x1^2 +x1*x2 +4*(x2^2)) <= a ) 
}
