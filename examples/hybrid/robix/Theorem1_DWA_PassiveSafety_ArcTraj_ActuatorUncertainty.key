/*
 *	Robot must safely follow the dynamic window navigation approach.
 *  Model for passive safety, circular trajectory, and actuator uncertainty.
 *  Theorem 1
 *	Proof see Theorem1_DWA_PassiveSafety_ArcTraj_ActuatorUncertainty.key.proof
 * 
 *  Invariant (psi_ps):
 *  - Robot drives forward
 *  - Direction vector of robot is a unit vector
 *  - Radius of circular trajectory is strictly positive
 *  - Robot is stopped or robot can still stop (accounting for uncertainty) before collision with obstacle occurs
 * 
 *  Safety property:
 *  - Robot is stopped or robot can still stop before collision with obstacle occurs  
 *
 *	@author Stefan Mitsch, Khalil Ghorbal, Andre Platzer
 *	@see On Provably Safe Obstacle Avoidance for Autonomous Robotic Ground Vehicles
 *	@link http://symbolaris.com/pub/robix.pdf
 */

\functions{
	R ep;			 		/* time limit for control decisions */
 	R b;					/* minimum braking capability of the robot */
	R A;					/* maximum acceleration -b <= a <= A */
	R V;					/* maximum velocity of obstacles */
	R Om;					/* max. rotational velocity for dynamic window */
	R U_m;					/* upper bound on the uncertainty about actual braking/acceleration */
}
\programVariables{
	R x;		/* robot position: x */
	R y;		/* robot position: y */
	R v;		/* robot linear velocity */
	R a;		/* robot linear acceleration */
	R ua;		/* acceleration uncertainty */
	R w;		/* robot angular velocity (= linear velocity) */
	R dx; 		/* robot orientation: x */
	R dy; 		/* robot orientation: y */
	R cx;		/* rotation center: x */
	R cy;		/* rotation center: y */
	R r;		/* radius of curvature */
	R ox;		/* position of obstacle */
	R oy;
	R odx;		/* direction and linear velocity of obstacle */
	R ody;
	R t;		/* time */
}

\problem{  
	  v >= 0
	& (Abs(x - ox) > v^2 / (2*b*U_m) + v*V / (b*U_m) | Abs(y - oy) > v^2 / (2*b*U_m) + v*V / (b*U_m))
	& r > 0
	& dx^2 + dy^2 = 1
	& A >= 0
	& b > 0
	& 0 < U_m & U_m <= 1
	& V >= 0
	& ep > 0	
      -> \[	(	/* control obstacle */
      			odx := *;
      			ody := *;
      			?odx^2 + ody^2 <= V^2;
      
      			/* brake or remain stopped on your current curvature */
      			(a := -b)
      			++(?v = 0; a := 0; w := 0)
      			/* or choose a new safe curvature */
				++( /* Inside dynamic window? */
					a := *; ?-b <= a & a <= A; /* cut -b <= a <= 0 | 0 <= a <= A to make diff. inv. possible */
					
					cx := *;		
					cy := *;
					r := *; ?r > 0 & r^2 = (x - cx)^2 + (y - cy)^2; /* do not rotate around own axis */
					w := *; ?w * r = v & -Om <= w & w <= Om;
					
					/* uncomment for omnidirectional drive */	
					/*dx := -1/r * (y - cy);
					dy := 1/r * (x - cx);*/
					
					/* for the chosen a, w, cx, cy: nearest obstacle on that curvature */
					ox := *;
					oy := *;
					
					/* use that curvature, if it is a safe one (admissible velocities) */
					?Abs(x - ox) > v^2/(2*b*U_m) + V*v/(b*U_m) + (A/(b*U_m) + 1) * (A/2 * ep^2 + ep*(v+V))
						 | Abs(y - oy) > v^2/(2*b*U_m) + V*v/(b*U_m) + (A/(b*U_m) + 1) * (A/2 * ep^2 + ep*(v+V))
				);
				t := 0;	
				
				/* uncertainty about acceleration/braking */
				ua := *; ?U_m <= ua & ua <= 1;
				
				/* dynamics */
				{x' = v * dx, y' = v * dy, dx' = -w * dy, dy' = w * dx, v' = ua*a, w' = ua*a * 1/r,
						ox' = odx, oy' = ody,
						t' = 1, t <= ep, v >= 0
				}
				@invariant(
						t >= 0,  
						dx^2 + dy^2 = 1
						/* proof hint: overapproximate 2-norm with infinity-norm (asymmetric for braking and acceleration) */
						/*(a <= 0 & v0 + a*t <= v & v <= v0 + a*U_m*t) | (a >= 0 & v0 + a*U_m*t <= v & v <= v0 + a*t),
						(a <= 0 & -t * (v0 + a*U_m/2*t) <= x - x0 & x - x0 <= t * (v0 + a*U_m/2*t)) | (a >= 0 & -t * (v0 + a/2*t) <= x - x0 & x - x0 <= t * (v0 + a/2*t)),
						(a <= 0 & -t * (v0 + a*U_m/2*t) <= y - y0 & y - y0 <= t * (v0 + a*U_m/2*t)) | (a >= 0 & -t * (v0 + a/2*t) <= y - y0 & y - y0 <= t * (v0 + a/2*t)),
						-t * V <= ox - ox0 & ox - ox0 <= t * V,
						-t * V <= oy - oy0 & oy - oy0 <= t * V,
						*/
					)
			)*
			@invariant(v >= 0
				& dx^2 + dy^2 = 1
				& r > 0
				& (v = 0 | Abs(x - ox) > v^2 / (2*b*U_m) + V * v/(b*U_m) | Abs(y - oy) > v^2 / (2*b*U_m) + V * v/(b*U_m))
			)
		\] (v = 0 | (x - ox)^2 + (y - oy)^2 > (v^2/(2*b))^2)				
}