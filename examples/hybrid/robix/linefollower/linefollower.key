\functions {
	R A;	/* maximum acceleration */
	R B;	/* braking force */
	R ep;	/* delay */
	R S;	/* charging station position */
}

\programVariables {
	R p;	/* position */
	R v;	/* velocity */
	R a;	/* acceleration */
	R t;	/* time */
}

/*
 * Robot has to stop at the latest at the charging station.
 * 
 * @provable automatically in KeYmaera using Mathematica
 */
\problem {
	  A >= 0
	& B > 0
	& ep > 0
	& v >= 0
	& p + v^2/(2*B) <= S 
	->
	\[
		(
		   /* Brake unconditionally */
		   (a := -B)
		   /* Stay stopped */												
		++ (?v = 0; a := 0)											
		   /* Coast */
		++ (?p + v^2/(2*B) + v*ep <= S; a := 0)						
		   /* Safe, any acceleration in [-B,A] */
		++ (?p + v^2/(2*B) + (A/B + 1)*(A/2*ep^2 + ep*v) <= S; 		
			a := *; ?-B <= a & a <= A);
		
		/* Reset timer */
		t := 0;
		/* Dynamics, execute next ctrl with delay <= ep */
		{ p' = v, v' = a, t' = 1 & v >= 0 & t <= ep}
		)*
		@invariant(
			  v >= 0
			& p + v^2/(2*B) <= S 
		)
	\] (p <= S)
}