\programVariables {
  R x, v, a, B, A, StopSign;
}
/**
 * Car stopping for a stop sign
 * 
 * The car starts with position and velocity such that,
 * if it were to apply the brakes, it would be able to come to 
 * a complete stop before reaching the stop sign.  
 * 
 * Variable Definitions:
 * x position of car
 * v velocity of car
 * a acceleration of car
 * A max acceleration 
 * B max braking 
 * StopSign position of Stop Sign
 */

\problem{
    (  v >= 0  
	 & B  > 0
	 & A  > 0 
     & x + v^2/(2*B) <= StopSign)
->
  \[
    (
	  ((
	    
	    ?(x + v^2/(2*B) <= StopSign); a := A) 
	   ++ a := -B
	  ++ (?(v = 0); a := 0));
      {x' = v, v' = a, 
	  v >= 0 & x + v^2/(2*B) <= StopSign  
	  }
    )*@invariant(x + v^2/(2*B) <= StopSign)
  \] (x <= StopSign) 
}

