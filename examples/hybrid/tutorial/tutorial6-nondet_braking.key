\programVariables {
	R p, v, a, A, STOP_SIGN, t, ep, b, B;
}
/* 
*  Simple example of a car stopping in front of a stop sign
*
*  Should prove automatically
*/

\problem{


( ep > 0 & v >= 0 & b > 0 & B > b & A > 0 &
	(p + (1/2)*(v^2/b) <= STOP_SIGN)
	) ->

\[(

	/*discrete, or ctrl*/
	(
	(?( STOP_SIGN >= p + v^2/(2*b)+(A/b + 1)*(A/2*ep^2 + ep*v) );
		a := *; ?( a >= -B); ?(a <= A)
	)
	++(  
	( a:=* ); ?( a <= - b); ?( a >= -B )
	)
	++( ?(v=0); a:=0)
	)
	;t := 0

	/* Junction between discrete and continuous */
	;	

	/*continous, or dyn*/
	{t' = 1, p' = v, v' = a & v >= 0 & t <= ep}

)*@invariant(
			(1/2)*(v^2/b) + p <= STOP_SIGN
 	)\] ( p <= STOP_SIGN )

}
