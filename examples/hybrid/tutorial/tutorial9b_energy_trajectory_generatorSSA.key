/* 
* SSA example with a trajectory generator for the PD controller,
* so that car inches forward towards the intersection.
*
* Should prove automatically
*
* Author: Nikos Arechiga
*/

\problem{

\[R p, v, a, S, Kp, Kd, c, ref, pmeasured, t, tau\] (

	( 
		v >= 0
		& Kp = 2 & Kd = 3
		& tau > 0
		
		& c > 0
		& pmeasured = p
		& ref = pmeasured/2 + S/2
		& (5/4)*(p - ref)^2 + ((p-ref)*v)/2 + v^2/4 < (S/2 - pmeasured/2)^2

	) ->

\[(

	(pmeasured := p;
	ref := pmeasured/2 + S/2;
	
	{
		t' = 1,
		p' = v, 
		v' = -Kp*( p - ref ) - Kd*v

		& t <= tau
		& v >= 0 
	}


 	))*@invariant(
 		(5/4)*(p-ref)^2 + ((p-ref)*v)/2 + v^2/4 < (S/2 - pmeasured/2)^2	
 	)\] ( 
 		p <= S
	)

)
}
