/**
 * Car stopping for a stop sign - proves automatically
 * 
 * In this example, the control for the car does not prevent the car
 * from running the stop sign; however, by looking at branches of the
 * proof which are not true, we may see what properties are required 
 * in the initial conditions and the controller in order to prove safety.
 * 
 */
\functions {
	R A;			/* max acceleration  */
	R B;			/* max braking */
	R eps;			/* max time between discrete control loops */
	R StopSign;		/* position of Stop Sign */
} 
 
\programVariables {
	R x;	/* position of car */
	R v;	/* velocity of car */
	R a;	/* acceleration of car */
	R t;	/* time */
}

\problem{
    (  v >= 0  
	 & B  > 0
	 & A  > 0 
	 & eps > 0
	 & x + v^2/(2*B) <= StopSign)
->
  \[
    (
	  (  (?(x + v^2/(2*B) + (A/B + 1)*(A/2*eps^2 + eps*v) <= StopSign); a := A)
	  ++ a := -B
	  ++ (?(v = 0); a := 0));
      t := 0;{x' = v, v' = a, t' = 1,
	  v >= 0, t < eps
	  })*@invariant(x + v^2/(2*B) <= StopSign)
    
  \] (x <= StopSign)
}

