

\problem{

	\[
	R v;			/* velocity */
	R DeltaV;		/* Difference of current velocity to desired velocity */
	R vref;			/* desired velocity */
	R Gp;			/* Gear we are in right now*/
	R Kp;			/* Proportional gain of controller */
	R m;			/*mass of the car */
	R DeltaTI;		/*= TI, the integral part of the ctrl signal */
	R TR;			/* A constant chosen to have fast convergence with low overshoot*/
	R G1, G2, G3, G4;
	R K1, K2, K3, K4;
	R omega_high, omega_low;
	R omega_max;
	R DV_engage;
	R level;

	m := 1500; TR := 40;
	G1 := 50; G2:= 32; G3 := 20; G4 := 14;
	K1 := 3.75; K2 := 5.86; K3 := 9.37; K4 := 13.39;
	vref := 30;
	
	omega_high := 3500;
	omega_low := 1500;
	omega_max := 7000;

	DV_engage := 15;

	level := 58000;

	\]
	((
	DeltaTI = 0	
	& -DV_engage <= DeltaV & DeltaV <= DV_engage 
	& Gp = G1
	)->
	\[(
	v := DeltaV + vref;

	(
	/* Shift up a gear if engine is going too fast */
	(?Gp != G4; ?v >= (1/Gp)*omega_high;
		/* Increase gear */
		(
		(?Gp = G1; Gp := G2)
		++ (?Gp = G2; Gp := G3)
		++ (?Gp = G3; Gp := G4)
		)
		)
	++	
	/* Shift down a gear if engine is going too slow */
	(?Gp != G1; ?v <= (1/Gp)*omega_low;
		/* Increase gear */
		(
		(?Gp = G4; Gp := G3)
		++ (?Gp = G3; Gp := G2)
		++ (?Gp = G2; Gp := G1)
		)
		)
	)
	;	
	Kp := 187.5/Gp;
	{ 
	DeltaV' = (-Gp*Kp/m)*DeltaV - (Gp/m)*DeltaTI,
	DeltaTI' = (Kp/TR)*DeltaV
	& ( (DeltaV + vref <= (1/Gp)*omega_high | Gp = G4)
	& (DeltaV + vref >= (1/Gp)*omega_low | Gp = G1) )
	}

	)*\] ( Gp*(DeltaV + vref) <= omega_max ))

}

/* Proposed Lyapunov function/invariant, level = 58000
40.822*DeltaTI^2 + 144.524*DeltaTI*DeltaV + 255.589*DeltaV^2 <= level & ( Gp*(DeltaV + vref) > omega_high -> Gp = G4  )
*/

