/**
 * Simple example of a controlled moving point as a hybrid system.
 * Point x is moving on a line towards 0.
 */

\problem {
  /* state variable declarations */
  \[ R x, a, t, c; \] (
      /* initial state characterization */
      x^2 < (4*c)^2
  ->
     \[                           /* system dynamics */
       (
        if (x>0) then
            a := -4               /* move left */
        else
            a := 4                /* move right */
        fi;
        t := 0;                   /* reset clock variable t */
        {x'=a, t'=1, t<=c}        /* continuous evolution */
       )*@invariant(x^2<=(4*c)^2) /* repeat these transitions */
     \] (x^2 <= (4*c)^2)          /* safety / postcondition */
  )
}
