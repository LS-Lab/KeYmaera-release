/**
 * Essentials of European Train Control System (ETCS)
 * @see "Andre Platzer. Differential dynamic logic for hybrid systems. Journal of Automated Reasoning, 41(2), pages 143-189, 2008."
 * @provable
 */
\functions{
  R ep;
  R b;
  R A;
}

\programVariables {
  R SB, a, v, z, t, m;
}

\problem {
    ( v^2 <= 2*b*(m-z) & b > 0 & A>=0)
  -> 
   \[(
    SB := (v^2)/(2*b) + ((A/b) + 1)*((A/2)*ep^2 + ep*v);
    ((?m - z <= SB; a:= -b) ++ (?m - z >= SB; a:=A));
    (t:=0; {z'=v, v' = a, t'=1, (v >= 0 & t <= ep)})
    )*@invariant(v^2<=2*b*(m-z))\] (z <= m)
}
