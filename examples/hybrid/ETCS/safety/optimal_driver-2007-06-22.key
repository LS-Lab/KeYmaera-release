\sorts{
R;
}

\functions {
R b;
R amax;
R ep;
}

/*
changes: {state,do,SB,t,a,m,vdes,ST,v,mo,d,z}
invariant:
v^2 - d^2 <= 2*b*(m-z) & v >= 0 & d >= 0
*/

\problem {
\[ R vdes; R SB; R mo; R t; R a; R v; R z; R m; R drive; R brake; R state; R d; R do; drive := 0; brake := 1; state:= drive \] ((  v^2 - d^2 <= 2*b*(m-z) & v >=0 & ep > 0 & b > 0 & amax > 0 & d >= 0)
-> 
 \[
(
SB := ((amax/b)+1)*ep*v+((v^2 - d^2)/(2*b)) + ((amax/b)+1)*amax*ep^2/2;
(
		(
		(vdes:=*; ?vdes > 0) 
		++ (do:= d; mo:=m; m := *; d := *; ?d >= 0 & do^2 - d^2 <= 2*b*(m-mo))
		++ (state := brake)
		)

	++  
		(((?state = brake; a:= -b)
		++ (?state = drive; 
			(a:=(vdes-v)/ep; ((?a >=-b & a <= amax) ++ (?a < -b; a := -b) ++ (?a > amax; a := amax)))));
			
			
		((?m - z <= SB; a:= -b) ++ (?m - z > SB));
		(t:=0; {z'=v, v' = a, t'=1, (v >= 0 & t <= ep)}))
)
)*@invariant(v^2-d^2 <= 2*b*(m-z) & v >= 0 & d >= 0)
\] 
(z >= m -> v <= d))
}
