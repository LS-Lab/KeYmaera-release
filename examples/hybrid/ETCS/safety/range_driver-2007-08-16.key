\sorts{
R;
}

\functions {
R b;
R amax;
R ep;
}

\problem {
\[ R vdes; R SB; R SB0; R Pa; R Pe; R Pd; R MAa; R MAe; R MAd; R t; R a; R v; R z; R drive; R brake; R state; drive := 0; brake := 1; state:= drive \] ((  v^2 - MAd^2 <= 2*b*(MAa-z) & v^2 <= 2*b*(MAe-z) & v >=0 & ep > 0 & b > 0 & amax > 0 & MAd >= 0 & Pd >= 0 & v <= Pd & z >= Pa & z <= Pe)
-> 
 \[
( 
if(z >= MAa) then
	Pa := MAa;
	Pe := MAe;
	Pd := MAd
fi;
SB := ((amax/b)+1)*ep*v+((v^2 - MAd^2)/(2*b)) + ((amax/b)+1)*amax*ep^2/2;
SB0 := ((amax/b)+1)*ep*v+(v^2/(2*b)) + ((amax/b)+1)*amax*ep^2/2;
(
		(
		(vdes:=*; ?vdes > 0) 
		++ (MAa := *; MAe:=*; MAd := *; ?MAd >= 0 & MAa <= MAe & v^2 - MAd^2 <= 2*b*(MAa-z) & v^2 <= 2*b*(MAe-z))
		++ (state := brake)
		)

	++ ( 
		if(state = brake) then a:= -b else 
			((?v <= vdes; a:=*; ?a >=0 & a <= amax) 
			++ (?v >= vdes; a:=*; ?a <=0 & a >= -b)) 
		fi;
		if(z >= Pa & z <= Pe) then 
			a:=*; 
			?a >= -b & a*ep <= Pd - v & a <= amax
		fi;
		if(MAa - z <= SB | MAe - z <= SB0) then 
			a:= -b 
		fi;
		(t:=0; {z'=v, v' = a, t'=1, (v >= 0 & t <= ep)}))
)
/* invariant:  v^2 - MAd^2 <= 2*b*(MAa-z) & v^2 <= 2*b*(MAe-z) & v >= 0 & MAd >= 0 & Pd >=0 & ((z >= Pa & z <= Pe) -> v <= Pd) & MAa <= MAe */
)* 
\] 
((z >= MAe -> v <= 0) & (z >= MAa -> v <= MAd) & ((z >= Pa & z <= Pe) -> v <= Pd)))
}
