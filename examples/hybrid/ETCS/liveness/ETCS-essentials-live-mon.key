/**
 * Essentials of European Train Control System (ETCS)
 * Liveness analysis with monotonicity relaxation.
 */
\functions{
  R ep;
  R b;
  //R A;
}

/**
 * @variant(z + n*ep*vo>=p_0 & v>=vo, n) provable
 * @variant((z<p_0 -> z + n*ep*v>=p_0 & v=vo) & v>=0, n) for A=0 even without n>0
 * During the proof it automatically finds a choice for m.
 * Alternatively: manual instatiation of m for A=0 with
 *   p_0 + v^2/(2*b) + ep*v
 *   CAVEAT(for manual instantion of m): vo is chosen which happens to be replaced by v in the current proof, but could look different in general.
 * Quicker proving interactions:
 *   hiding SB constraint on m-z>=SB branch and universal elimination
 *   choose t=0 on m-z<SB branch
 */
\problem {
\[
  R SB, a, v, z, t, m ; R vo; R A; A:=0 \] (
    v>0 & ep>0 & b>0 & A>=0
  ->
    \forall R p; 
    \[vo := v\]
    \<m := *\>
    \<(SB := v^2/(2*b) + (A/b + 1) * (A/2*ep^2 + ep*v);
       if(m - z >= SB) then
           t:=0;
           {z'=v,v'=A,t'=1}; ?t<=ep
       else
           t:=0;
           {z'=v,v'=-b,t'=1}; ?v>=0 & t<=ep
       fi
      )*@variant($n, z + $n*ep*vo>=p & v=vo)\> z >= p
  )
}