/**
 * Essentials of European Train Control System (ETCS)
 * Liveness analysis.
 */
\functions{
  R ep;
  R b;
  R A;
}

/**
 * @variant($n, z + $n*ep*vo>=p_0 & v=vo) provable for A=0
 * @variant($n, (z<p_0 -> z + $n*ep*v>=p_0 & v=vo) & v>=0) for A=0 even without n>0
 * During the proof it automatically finds a choice for m.
 * Alternatively: manual instatiation of m for A=0 with
 *   p_0 + v^2/(2*b) + ep*v
 *   CAVEAT(for manual instantion of m): vo is chosen which happens to be replaced by v in the current proof, but could look different in general.
 * Quicker proving interactions:
 *   hiding SB constraint on m-z>=SB branch and universal elimination
 *   choose t=0 on m-z<SB branch
 */
\problem {
\[
  R SB, a, v, z, t, m, d, vdes; R vo, zo, mo, do \] (
    v>=0 & ep>0 & b>0 & A>0 
  ->
    \forall R p; (p > 0 -> (
    \<
	(do:= d; mo:=m; m := *; d := *; vdes:=*; ?d >= 0 & do^2 - d^2 <= 2*b*(m-mo) & vdes >= 0; 
	SB := (v^2-d^2)/(2*b) + (A/b + 1) * (A/2*ep^2 + ep*v);
	   a:= A;
       if(m - z <= SB) then
	   	   a:=-b
	   fi;
       t:=0;
       {z`=v,v`=a,t`=1, v>=0 & t<=ep})\> (v > 0)
	 & (v > 0 ->
    \[z:=0;zo:=z; vo:=v\]
	\<(do:=d; mo:=m; m := *; d := *; vdes:=*; ?d >= 0 & do^2 - d^2 <= 2*b*(m-mo) & vdes >= 0; 
	SB := (v^2-d^2)/(2*b) + (A/b + 1) * (A/2*ep^2 + ep*v);
	   a:= A;
       if(m - z <= SB) then
	   	   a:=-b
	   fi;
       t:=0;
       {z`=v,v`=a,t`=1, v>=0 & t<=ep}
      )*
       @variant($n, z + $n*ep*vo >= p & v>=vo)
       /*@variant($n, z + $n*A/2*ep^2 -A/2*ep^2>= p & v>=0)*/
       /*@variant($n, z + $n*ep*vo >= p & v>=vo)*/
       /*@variant($n, z + $n*ep*vo >= p & v^2<=vo^2+2*A*(p-zo)  -$n*A*ep)*/
       /*@variant($n, z + A/2*($n*ep)^2+$n*ep*vo >= p & v^2<=vo^2+2*A*(p-zo)-$n*A*ep)*/
    \> z >= p)))
  )
}
