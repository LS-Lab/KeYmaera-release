/**
 * @provable (with diffreplace)
 * @see "Andre Platzer and Jan-David Quesel. European Train Control System: A case study in formal verification. In Karin Breitman and Ana Cavalcanti, editors, 11th International Conference on Formal Engineering Methods, ICFEM, Rio de Janeiro, Brasil, Proceedings, volume 5885 of LNCS, pages 246-265. Springer, 2009."
 */
\functions {
R b;
R A;
R ep;
R l;
R u;
}

\programVariables {
R vdes, SB, mo, t, a, v, z, m, d, do, drive, brake, state; 
}

/* is v>=0 superfluous and is ep>=0 & A>=0 sufficient in place of ep>0&A>0? */
\problem {
\[ drive := 0; brake := 1; state:= drive \] ((v >=0 & d >= 0 & b > u & u >= 0 & l >= 0 & ep > 0 & A > 0)
-> (
      (SB >= (v^2 - d^2) /(2*(b-u)) + ((A+u)/(b-u) + 1) * ((A+u)/2 * ep^2 + ep * v))
     ->
        \forall R m1; \forall R z1; \[m:=m1; z:=z1\] (v^2 - d^2 <= 2*(b-u)*(m-z) & m-z>= SB -> 
        \[
            (t:=0; {z'=v, A-l<=v',v' <= A+u, t'=1, v>=0 & t<=ep}
             @invariant(m-z >= (v^2-d^2)/(2*(b-u)) + (((A+u)/(b-u)) + 1)*(((A+u)/2)*(ep-t)^2 + (ep-t)*v));
            ?(v >= 0 & t <= ep))
        \] (v^2-d^2 <= 2*(b-u)*(m-z)))
   ) & ((
        \forall R m1; \forall R z1; \[m:=m1; z:=z1\] (v^2 - d^2 <= 2*(b-u)*(m-z) & m-z>= SB -> 
        \[
            (t:=0; {z'=v, A-l<=v',v' <= A+u, t'=1};
            ?(v >= 0 & t <= ep))
        \] (v^2-d^2 <= 2*(b-u)*(m-z)))
    ) ->
      (SB >= (v^2 - d^2) /(2*(b-u)) + ((A+u)/(b-u) + 1) * ((A+u)/2 * ep^2 + ep * v))
   )
)
}
