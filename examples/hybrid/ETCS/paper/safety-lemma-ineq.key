\settings {
"[DLOptions]diffSatTimeout=40000"
}
/**
 * Safety lemma for European Train Control System
 * @provable
 * The following refinement is provable, too: SB := (v^2 - d^2)/(2*b) + (a/b+1)*(A/2*ep^2+ep*v);
 * @see "Andre Platzer and Jan-David Quesel. European Train Control System: A case study in formal verification. In Karin Breitman and Ana Cavalcanti, editors, 11th International Conference on Formal Engineering Methods, ICFEM, Rio de Janeiro, Brasil, Proceedings, volume 5885 of LNCS, pages 246-265. Springer, 2009."
 */
\functions {
R b;
R A;
R ep;
}

\problem {
\[ R vdes, SB, mo, t, a, v, z, m, d, do, drive, brake, state; drive := 0; brake := 1; state:= drive \] ((  v^2 - d^2 <= 2*b*(m-z) & d >=0 & b > 0 & A >= 0 & ep >= 0)
-> 
 \[
( 
       (
        (do:= d; mo:=m; m := *; d := *; vdes:=*; ?d >= 0 & do^2 - d^2 <= 2*b*(m-mo) & vdes >= 0)
        ++ (state := brake)
       )
    ++ (
        ((?v <= vdes; a:=*; ?a >=-b & a <= A) 
        ++ (?v >= vdes; a:=*; ?a <0 & a >= -b));
        SB := (v^2 - d^2)/(2*b) + (A/b+1)*(A/2*ep^2+ep*v);
        if (m - z <= SB | state = brake) then a:= -b fi;
        (t:=0;{z'=v, v' <= a, v'>=-b, t'=1, (v >= 0 & t <= ep)}
         @candidate(m-z >= (v^2-d^2)/(2*b) + (A/b + 1)*(A/2*(ep-t)^2 + (ep-t)*v)))
       )
)*@invariant(v^2 - d^2 <= 2*b*(m-z) & d >= 0)
\] 
(z >= m -> v <= d))
}
