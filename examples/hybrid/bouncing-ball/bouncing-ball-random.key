\programVariables {
R h,v,t; 
R c,g,H,V;
}

/**
 * Hybrid bouncing ball example.
 * h = height
 * v = velocity
 * H = height limit
 * g = gravitation
 * c = elastic dampening factor at floor (h=0)
 * provable
 * additional invariant(&t<=2*V/g)
 */
\problem {
    (g>0 & h=g/2*t^2+v*t&h>=0&t>=0&v<=-g*t+V)
  ->
   \[
    (
      {h'=v,v'=-g,t'=1, h>=0};
      if (t>0&h=0) then
      	c:=*; ?(0<=c&c<1);
        v := -c*v; t:=0
      fi
    )*@invariant(h=g/2*t^2+v*t&h>=0&t>=0&v<=-g*t+V)
   \] (0<=h&h<=V^2/(2*g))
}
