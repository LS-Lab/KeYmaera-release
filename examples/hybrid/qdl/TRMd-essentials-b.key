/**
 * Essentials of Distributed Tangential Roundabout Maneuver (TRM) in Air Traffic Control (ATC)
 * @author Andre Platzer
 * @see "Andre Platzer. Quantified differential invariants. In Emilio Frazzoli and Radu Grosu, editors, Proceedings of the 14th ACM International Conference on Hybrid Systems: Computation and Control, HSCC 2011, Chicago, USA, April 12-14, Pages 63-72. ACM, 2011"
 */

 \sorts {
 A;    /* aircraft */
 }

 \programVariables {
   R c1,c2, omc;
 }

\functions {
  R protectedzone;
\nonRigid[Location] R x1(A);
\nonRigid[Location] R x2(A);
\nonRigid[Location] R d1(A);
\nonRigid[Location] R d2(A);
\nonRigid[Location] R om(A);
}


\problem {
    \forall A i. \forall A j. (x1(i)-x1(j))^2 + (x2(i)-x2(j))^2 >= protectedzone^2
  ->
   \[(
       /*(
         om:=*;omy:=*;
         {x1'=d1,x2'=d2, d1'=-om*d2,d2'=om*d1, y1'=e1,y2'=e2, e1'=-omy*e2,e2'=omy*e1,
          ((x1-y1)^2 + (x2-y2)^2 >= protectedzone^2)}@weaken()
       )*@invariant((x1-y1)^2 + (x2-y2)^2 >= protectedzone^2);
       */
       c1:=*;c2:=*; omc:=*;
       \forall A i. d1(i):=-omc*(x2(i)-c2);
       \forall A i. d2(i):=omc*(x1(i)-c1);
       \forall A i. {x1(i)'=d1(i),x2(i)'=d2(i), d1(i)'=-omc*d2(i),d2(i)'=omc*d1(i)}
       @invariant(\forall A i. (d1(i)=-omc*(x2(i)-c2)&d2(i)=omc*(x1(i)-c1)))
    )*@invariant(\forall A i. \forall A j. (x1(i)-x1(j))^2 + (x2(i)-x2(j))^2 >= protectedzone^2)\] (
        \forall A i. \forall A j. (x1(i)-x1(j))^2 + (x2(i)-x2(j))^2 >= protectedzone^2
    )
}
